*** Settings ***
Library  plone.act.Zope2ServerLibrary
Library  plone.act.PloneLibrary
Library  Selenium2Library  run_on_failure=Capture Page Screenshot

Resource  plone/act/keywords.txt



*** Keywords ***

# ----------------------------------------------------------------------------
# Deco Editor Interaction
# ----------------------------------------------------------------------------

Wait For Toolbar
    Wait Until Page Contains Element  id=toolbar
    Select Frame  id=toolbar
    Wait Until Keyword Succeeds  3  5  Current Frame Contains  Edit

Open Deco Editor on
    [Arguments]  ${page_url}

    Log in as site owner
    Go to  ${page_url}

    Wait For Toolbar
    Element Should Be Visible  id=plone-toolbar
    Element Should Not Be Visible  id=deco-toolbar
    Click Element  xpath=id('plone-action-deco')/a
    Wait Until Keyword Succeeds  1  5  Element Should Be Visible  id=deco-toolbar

Close Deco Editor
    Element Should Be Visible  id=plone-toolbar
    Element Should Be Visible  id=deco-toolbar
    Click Element  xpath=id('plone-action-deco')/a
    Wait Until Keyword Succeeds  1  5  Element Should Not Be Visible  id=deco-toolbar

Get XOffset From Toolbar To Content
    [arguments]  ${toolbarsrc}  ${contentdest}  ${adjust}=0
    ${srcxpos} =  Get Horizontal Position  ${toolbarsrc}
    Unselect Frame
    ${destxpos} =  Get Horizontal Position  ${contentdest}
    ${xoffset} =  Get Offset Difference  ${srcxpos}  ${destxpos}  ${adjust}
    [Return]  ${xoffset}

Get YOffset From Toolbar To Content
    [arguments]  ${toolbarsrc}  ${contentdest}  ${adjust}=0
    ${srcypos} =  Get Vertical Position  ${toolbarsrc}
    Unselect Frame
    ${destypos} =  Get Vertical Position  ${contentdest}
    ${yoffset} =  Get Offset Difference  ${srcypos}  ${destypos}  ${adjust}
    [Return]  ${yoffset}

Drag From Toolbar And Drop On Content
    [arguments]  ${src}  ${dest}  ${xadjust}=0  ${yadjust}=0
    ${xoffset} =  Get XOffset From Toolbar To Content  ${src}  ${dest}  ${xadjust}
    Select Frame  id=toolbar
    ${yoffset} =  Get YOffset From Toolbar To Content  ${src}  ${dest}  ${yadjust}
    Select Frame  id=toolbar
    Drag And Drop By Offset  ${src}  ${xoffset}  ${yoffset}
