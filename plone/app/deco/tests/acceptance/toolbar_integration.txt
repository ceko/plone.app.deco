*** Settings ***

Library  plone.act.PloneLibrary
Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Variables  plone/app/testing/interfaces.py
Resource  plone/act/keywords.txt
Resource  keywords.txt

Suite Setup  Open browser  ${PLONE_URL}

Suite Teardown  Close All Browsers


*** Variables ***

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox
${TEST_FOLDER} =  ${PLONE_URL}/acceptance-test-folder
${TEST_PAGE} =  ${TEST_FOLDER}/test-deco-page


*** Test cases ***

Deco page has toolbar
    Log in as site owner

    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Page Should Contain Element  id=plone-toolbar
    Capture Page Screenshot  filename=../../../../docs/_images/selenium-screenshot-test.png

Deco page has deco toolbar
    Log in as site owner

    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Page Should Contain Element  id=deco-toolbar
    Element Should Not Be Visible  id=deco-toolbar

Edit button toggles deco toolbar
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Click Element  xpath=id('plone-action-deco')/a

    # we should not be at the usual edit url
    Location Should Be  ${TEST_PAGE}
    Element Should Be Visible  id=deco-toolbar

    Click Element  xpath=id('plone-action-deco')/a

    # still no change in URL, deco tools should disappear
    Location Should Be  ${TEST_PAGE}
    Wait Until Keyword Succeeds  3  5  Element Should Not Be Visible  id=deco-toolbar

Deco toolbar contains save button
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Open Deco Editor

    Element Should Be Visible  id=deco-toolbar-save

Deco toolbar contains cancel button
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Open Deco Editor

    Element Should Be Visible  id=deco-toolbar-cancel

Deco toolbar contains add-column button
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Open Deco Editor

    Element Should Be Visible  id=plone-deco-addcolumn

Deco toolbar contains add-row button
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Open Deco Editor

    Element Should Be Visible  id=plone-deco-addrow

Deco toolbar contains add-tile form
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar

    Open Deco Editor

    Element Should Be Visible  name=add-tile

Tile can be dragged to column
    Log in as site owner
    Go to  ${TEST_PAGE}
    Wait For Toolbar
    Open Deco Editor

    Drag And Drop By Offset  xpath=//*[@id="plone-tiletype-plone-app-texttile"]  60  200
    Sleep  10
    Wait Until Page Contains Element  id=add_tile
    Element Should Contain  id=add_tile  Add Text tile

